
	' Original program by MrDave6309
	' Modifications by Rick Adams

	' Exit on BREAK key
	on brk goto 1000

	' Load 8k BMP graphic files into MMU blocks 30 to 42
	for i = 65 to 77
		poke &hffa2, i - 35
		f$ = "PLAT" + chr$(i) + ".BIN"
		print f$
		loadm f$
	next i

	' Fast CPU
	poke &hffd9, 0

	' Poke in ML code called by USR0
	read a, b
	for i = a to b
		read d
		poke i, d
	next i
	defusr0 = &h1000

	' Include DATA statements generated by compiling scroll.asm
	include "scroll.dat"

	' Set all palettes to zero while graphics are set up
	gosub 3000

	' Set up graphics
	hscreen 2
	poke &hff9f, &h80 ' 256 bytes per row

	' Set up scrolling
	vv = &h7800
	gosub 2000

	' Set all palettes to proper values
	gosub 4000

	' Scroll graphics into view
	for i = 1 to 220
		vv = vv + 32
		gosub 2000
	next i

	' Read keyboard
100	i$ = inkey$
	if i$ = "" then
		goto 100
	end if

	' Execute command
	i = asc(i$)
	if i = 94 then
		vv = vv - 32	' up
	end if
	if i = 95 then
		vv = vv - 64	' shift up
	end if
	if i = 10 then
		vv = vv + 32	' down
	end if
	if i = 91 then
		vv = vv + 64	' shift down
	end if
	if i = 8 then
		vv = vv - 1	' left
	end if
	if i = 21 then
		vv = vv - 2	' shift left
	end if
	if i = 9  then
		vv = vv + 1	' right
	end if
	if i = 93  then
		vv = vv + 2	' shift right
	end if
	if i$ = "M" then
		vv = vv + 129	' lower right
	end if
	if i$ = "B" then
		vv = vv + 127	' lower left
	end if
	if i$ = "Y" then
		vv = vv - 129	' upper left
	end if
	if i$ = "I" then
		vv = vv - 127	' upper right
	end if
	if i$ = "S" then
		palette 15, rnd(63) ' random palette shift
	end if

	' Clipping
	if vv > 37893 or vv < 30736 then
		vv = ov
	end if

	' Scroll screen
	gosub 2000

	' Keep going
	goto 100

	' Reset the machine
1000	poke &h71, 0
	exec &h8c1b

	' Scroll screen
2000	ov = vv
	v = usr0(vv)
	return

	' Set all palettes to zero
3000	for i = 0 to 15
		palette i, 0
	next i
	return

	' Set palettes to proper values
4000	for i = 0 to 15
		read c
		palette i, c
		data 34, 34, 3, 16, 5, 23, 42, 57, 46, 54, 58, 39, 10, 10, 11, 62
	next i
	return
